plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = "2.6.1"
group = project.mod_id

base {
	archivesName = project.mod_id
}

repositories {
	maven { url = "https://mvn.devos.one/snapshots/" }
}

loom {
	accessWidenerPath = file("src/main/resources/simplelibrary.accesswidener")

	mods {
		"simplelibrary" {
			sourceSet sourceSets.main
		}
	}
	
	runs {
		datagen {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=${mod_id}"
 
			runDir "build/datagen"
		}
	}
}

sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.api_version}"
	
	modApi include("io.github.fabricators_of_create.Porting-Lib:Porting-Lib:${project.porting_lib_version}")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}


jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications.register("mavenJava", MavenPublication) {
		from components.java
	}
}
