plugins {
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

version = "v1.0.0"
group = mod_id

base {
	archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

java.withSourcesJar()
java.withJavadocJar()

minecraft {
	mappings channel: "official", version: minecraft_version

	copyIdeResources = true

	runs {
		configureEach {
			workingDirectory project.file('run')
			
			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		client {
		}

		data {
			workingDirectory project.file('run-data')

			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
		}
	}
}

sourceSets.main.resources { 
	srcDir 'src/generated/resources' 
}

repositories {
	maven {
		url "https://maven.fabricmc.net/"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	
	implementation fg.deobf("net.fabricmc:fabric-loader:${fabric_version}")
	implementation fg.deobf("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")
}

tasks.named('jar', Jar).configure {
	manifest {
		attributes([
				'Specification-Title' : mod_id,
				'Specification-Vendor' : "Willatendo",
				'Specification-Version' : '1',
				'Implementation-Title' : project.name,
				'Implementation-Version' : project.jar.archiveVersion,
				'Implementation-Vendor' : "Willatendo",
				'Implementation-Timestamp' : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	finalizedBy 'reobfJar'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			groupId project.group
			artifactId project.archivesBaseName
			version project.version

			pom {
				name = 'Simple-Library'
				description = "Willatendo's library."
				url = 'https://github.com/willatendo/Simple-Library'
			}
		}
	}
}